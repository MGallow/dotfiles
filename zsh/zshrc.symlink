# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Set bindkey
bindkey -e

setopt extendedglob

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh~*/external*)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload -Uz compinit && compinit

# Prompt Setup
# Last update 2/21/2018
# Taken from Stack Overflow
# http://arjanvandergaag.nl/blog/customize-zsh-prompt-with-vcs-info.html
setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:git*' formats "(%b)"

zstyle ':vcs_info:*' enable git cvs svn

# or use precmd, see man zshcontrib
vcs_info_wrapper() {
  if [ -n "$vcs_info_msg_0_" ]; then
    return "%{$fg[grey]%}${vcs_info_msg_0_}%{$reset_color%}$del"
  fi
}

precmd() {vcs_info 2> /dev/null}

#RPROMPT=$'$(vcs_info_wrapper)'

PROMPT=$'%{\e]0;%d\a%}\n%F{green}%D{%y.%m.%f} %D{%L:%M:%S}%f %F{242}:%f %F{green}%n@%m:%d%f ${vcs_info_msg_0_}\n'

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

bindkey -s "^P" 'nvim +Files;^M'
bindkey -s "^G" 'nvim +Rg;^M'

tm() {   
	[[ -n "$TMUX" ]] && change="switch-client" || change="attach-session"
	if [ $1 ]; then
		tmux $change -t "$1" 2>/dev/null || (tmux new-session -d -s $1 && tmux $change -t "$1"); return
	fi   
	session=$(tmux list-sessions -F "#{session_name}" 2>/dev/null | fzf --exit-0) &&  tmux $change -t "$session" || echo "No sessions found." 
}

fzf_log() {
   hash=$(git log --color=always --format="%C(auto)%h%d %s %C(black)%C(bold)%cr" "$@" |  fzf | awk '{print $1}')
   echo $hash | xclip
   git showtool $hash 
}

export FZF_ALT_C_COMMAND="fd -t d"

export TERM='xterm-256color'

export FZF_COMPLETION_TRIGGER=''
bindkey '^T' fzf-completion
bindkey '^I' $fzf_default_completion
