# Make some sane history settings
HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000000
SAVEHIST=10000000
setopt BANG_HIST                 # Treat the '!' character specially during expansion.
setopt EXTENDED_HISTORY          # Write the history file in the ":start:elapsed;command" format.
setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY             # Share history between all sessions.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
setopt HIST_IGNORE_DUPS          # Don't record an entry that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS      # Delete old recorded entry if new entry is a duplicate.
setopt HIST_FIND_NO_DUPS         # Do not display a line previously found.
setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
setopt HIST_SAVE_NO_DUPS         # Don't write duplicate entries in the history file.
setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.
setopt HIST_VERIFY               # Don't execute immediately upon history expansion.
setopt HIST_BEEP                 # Beep when accessing nonexistent history.

# Vi mode
bindkey -v

zmodload zsh/complist

# use the vi navigation keys in menu completion
bindkey -M menuselect 'h' vi-backward-char
bindkey -M menuselect 'k' vi-up-line-or-history
bindkey -M menuselect 'l' vi-forward-char
bindkey -M menuselect 'j' vi-down-line-or-history

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

setopt extendedglob

# your project folder that we can `c [tab]` to
# export PROJECTS=/mnt/c/Users/Benjamin/drive/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh~*/external*)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

#Load Plugins
if [[ -s "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-autosuggestions/zsh-autosuggestions.zsh" ]]; then
   source "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-autosuggestions/zsh-autosuggestions.zsh"
fi

if [[ -s "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]]; then
   source "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
fi

if [[ -s "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-history-substring-search/zsh-history-substring-search.zsh" ]]; then
   source "${ZDOTDIR:-$HOME}/.dotfiles/external/zsh-history-substring-search/zsh-history-substring-search.zsh"
   #Vi mode command keys
   bindkey -M vicmd 'k' history-substring-search-up
   bindkey -M vicmd 'j' history-substring-search-down
fi
