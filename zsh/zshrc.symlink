# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Set bindkey
bindkey -v

setopt extendedglob

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh~*/external*)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload -Uz compinit && compinit

# Prompt Setup
# Last update 2/21/2018
# Taken from Stack Overflow
#  
setopt prompt_subst
autoload -Uz vcs_info
zstyle ':vcs_info:*' actionformats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{3}|%F{1}%a%F{5}]%f '
zstyle ':vcs_info:*' formats '%F{5}(%f%s%F{5})%F{3}-%F{5}[%F{2}%b%F{5}]%f '
zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b%F{1}:%F{3}%r'

zstyle ':vcs_info:*' enable git cvs svn

# or use precmd, see man zshcontrib
vcs_info_wrapper() {
  if [ -n "$vcs_info_msg_0_" ]; then
    return "%{$fg[grey]%}${vcs_info_msg_0_}%{$reset_color%}$del"
  fi
}

precmd() {vcs_info 2> /dev/null}

#RPROMPT=$'$(vcs_info_wrapper)'

PROMPT=$'%{\e]0;%d\a%}\n%F{green}%n@%m %F{yellow}%d%f $(vcs_info_wrapper)\nÎ» '
